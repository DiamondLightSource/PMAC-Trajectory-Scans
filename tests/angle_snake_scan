; ********************************************
; Parameters
; ********************************************

#DEFINE NUM_LOOPS P1
#DEFINE COUNTER P2
#DEFINE STEP P3
#DEFINE SMALL_STEP P4
#DEFINE OVER_STEP P5
#DEFINE ANGLE P6
#DEFINE X_STEP P7
#DEFINE X_SMALL_STEP P8
#DEFINE Y_STEP P9
#DEFINE Y_SMALL_STEP P10
#DEFINE X_OS P11
#DEFINE Y_OS P12


; ********************************************
; Set-Up
; ********************************************

DEL GAT	
; Coordinate System 1
&1
; Make sure all buffers are closed
CLOSE
; Assign motor 1 to the X-axis
#1->X
; Assign motor 2 to the Y-axis
#2->Y

; ********************************************
; Program
; ********************************************

OPEN PROG 1
CLEAR

; Set move modes
SPLINE1 TM10
;LINEAR
ABS

; Set move params
TA5
TS0
F10000

NUM_LOOPS = 100
COUNTER = 0
STEP = 1000
SMALL_STEP = 10
OVER_STEP = 5

ANGLE = 67
X_STEP = STEP*COS(ANGLE)
X_OS = OVER_STEP*COS(ANGLE)
X_SMALL_STEP = SMALL_STEP*SIN(ANGLE)

Y_STEP = STEP*SIN(ANGLE)
Y_OS = OVER_STEP*SIN(ANGLE)
Y_SMALL_STEP = SMALL_STEP*COS(ANGLE)


COMMAND"GAT"
WHILE(COUNTER < NUM_LOOPS)

	X(COUNTER*X_SMALL_STEP-X_STEP) Y(COUNTER*Y_SMALL_STEP+Y_STEP)
	X(COUNTER*X_SMALL_STEP-X_STEP-X_OS) Y(COUNTER*Y_SMALL_STEP+Y_STEP+Y_OS)
	X(COUNTER*X_SMALL_STEP-X_STEP-2*X_OS) Y(COUNTER*Y_SMALL_STEP+Y_STEP+2*Y_OS)
	COUNTER = COUNTER + 1
	X(COUNTER*X_SMALL_STEP-X_STEP-2*X_OS) Y(COUNTER*Y_SMALL_STEP+Y_STEP+2*Y_OS)
	X(COUNTER*X_SMALL_STEP-X_STEP-X_OS) Y(COUNTER*Y_SMALL_STEP+Y_STEP+Y_OS)
	
	X(COUNTER*X_SMALL_STEP-X_STEP) Y(COUNTER*Y_SMALL_STEP+Y_STEP)
	
	X(COUNTER*X_SMALL_STEP) Y(COUNTER*Y_SMALL_STEP)
	X(COUNTER*X_SMALL_STEP+X_OS) Y(COUNTER*Y_SMALL_STEP-Y_OS)
	X(COUNTER*X_SMALL_STEP+2*X_OS) Y(COUNTER*Y_SMALL_STEP-2*Y_OS)
	COUNTER = COUNTER + 1
	X(COUNTER*X_SMALL_STEP+2*X_OS) Y(COUNTER*Y_SMALL_STEP-2*Y_OS)
	X(COUNTER*X_SMALL_STEP+X_OS) Y(COUNTER*Y_SMALL_STEP-Y_OS)
	
	X(COUNTER*X_SMALL_STEP) Y(COUNTER*Y_SMALL_STEP)
	
ENDWHILE
COMMAND"END GAT"

X0 Y0

CLOSE
